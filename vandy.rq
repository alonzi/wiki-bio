# 2023-11-17
# This is an example test query from Lane

# run on 
# wikidataquery service - https://query.wikidata.org/#%23%20tool%3A%20scholia%0A%23%20title%3A%20Employees%20and%20affiliated%20with%20a%20specified%20organization%0A%0APREFIX%20target%3A%20%3Chttp%3A%2F%2Fwww.wikidata.org%2Fentity%2FQ29052%3E%0A%0ASELECT%0A%20%20%28SAMPLE%28%3Fnumber_of_works_%29%20AS%20%3Fworks%29%0A%20%20%28SAMPLE%28%3Fwikis_%29%20AS%20%3Fwikis%29%0A%20%20%3Fresearcher%20%3FresearcherLabel%20%3FresearcherDescription%0A%20%20%28SAMPLE%28%3Forcid_%29%20AS%20%3Forcid%29%0AWITH%20%7B%0A%20%20SELECT%20DISTINCT%20%3Fresearcher%20WHERE%20%7B%0A%20%20%20%20%3Fresearcher%20%28%20wdt%3AP108%20%7C%20wdt%3AP463%20%7C%20wdt%3AP1416%20%29%20%2F%20wdt%3AP361%2a%20target%3A%20.%0A%20%20%7D%20%0A%7D%20AS%20%25researchers%0AWITH%20%7B%0A%20%20SELECT%0A%20%20%20%20%28COUNT%28%3Fwork%29%20AS%20%3Fnumber_of_works_%29%20%3Fresearcher%0A%20%20WHERE%20%7B%0A%20%20%20%20INCLUDE%20%25researchers%0A%0A%20%20%20%20%23%20No%20biological%20pathways%3B%20they%20skew%20the%20statistics%20too%20much%20%0A%20%20%20%20MINUS%20%7B%20%3Fwork%20wdt%3AP31%20wd%3AQ4915012%20%7D%20%0A%0A%20%20%20%20%23%20This%20OPTIONAL%20query%20should%20be%20after%20the%20MINUS%20query%2C%20otherwise%0A%20%20%20%20%23%20researchers%20might%20not%20show%20if%20they%20do%20not%20have%20any%20papers.%0A%20%20%20%20OPTIONAL%20%7B%20%3Fwork%20wdt%3AP50%20%3Fresearcher%20.%20%7D%0A%20%20%7D%20%0A%20%20GROUP%20BY%20%3Fresearcher%0A%7D%20AS%20%25researchers_and_number_of_works%0AWHERE%20%7B%0A%20%20INCLUDE%20%25researchers_and_number_of_works%0A%20%20OPTIONAL%20%7B%20%3Fresearcher%20wdt%3AP496%20%3Forcid_%20.%20%7D%0A%20%20OPTIONAL%20%7B%20%3Fresearcher%20wikibase%3Asitelinks%20%3Fwikis_%20%7D%0A%20%20SERVICE%20wikibase%3Alabel%20%7B%20bd%3AserviceParam%20wikibase%3Alanguage%20%22en%2Cda%2Cde%2Ces%2Cfr%2Cnl%2Cno%2Cru%2Csv%2Czh%22%20.%20%7D%20%0A%7D%0AGROUP%20BY%20%3Fresearcher%20%3FresearcherLabel%20%3FresearcherDescription%20%0AORDER%20BY%20DESC%28%3Fworks%29%0A
# time - 6092 results in 3815 ms

# tool: scholia
# title: Employees and affiliated with a specified organization

PREFIX target: <http://www.wikidata.org/entity/Q29052>

SELECT
  (SAMPLE(?number_of_works_) AS ?works)
  (SAMPLE(?wikis_) AS ?wikis)
  ?researcher ?researcherLabel ?researcherDescription
  (SAMPLE(?orcid_) AS ?orcid)
WITH {
  SELECT DISTINCT ?researcher WHERE {
    ?researcher ( wdt:P108 | wdt:P463 | wdt:P1416 ) / wdt:P361* target: .
  } 
} AS %researchers
WITH {
  SELECT
    (COUNT(?work) AS ?number_of_works_) ?researcher
  WHERE {
    INCLUDE %researchers

    # No biological pathways; they skew the statistics too much 
    MINUS { ?work wdt:P31 wd:Q4915012 } 

    # This OPTIONAL query should be after the MINUS query, otherwise
    # researchers might not show if they do not have any papers.
    OPTIONAL { ?work wdt:P50 ?researcher . }
  } 
  GROUP BY ?researcher
} AS %researchers_and_number_of_works
WHERE {
  INCLUDE %researchers_and_number_of_works
  OPTIONAL { ?researcher wdt:P496 ?orcid_ . }
  OPTIONAL { ?researcher wikibase:sitelinks ?wikis_ }
  SERVICE wikibase:label { bd:serviceParam wikibase:language "en,da,de,es,fr,nl,no,ru,sv,zh" . } 
}
GROUP BY ?researcher ?researcherLabel ?researcherDescription 
ORDER BY DESC(?works)
